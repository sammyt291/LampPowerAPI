plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

description = 'LampPowerAPI'
group = 'org.finetree'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = uri('https://redempt.dev') }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.20.2-R0.1-SNAPSHOT"
    implementation 'com.github.Redempt:RedLib:6.5.8'
}

java.sourceCompatibility = JavaVersion.VERSION_17

shadowJar {
    archiveFileName = project.description + "-" + version + ".jar"
    relocate 'redempt.redlib', 'org.finetree.lamppowerapi.redlib'

    // Check if running in GitHub Actions
    if (System.getenv("GITHUB_ACTIONS") == null) {
        destinationDirectory = file('X:/plugins')
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}

processResources {
    def props = [version: version, name: project.description]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}